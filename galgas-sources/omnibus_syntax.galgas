syntax omnibus_syntax (lexicalAnalyzer) {

#--------------------------------------------------------------------*

rule <start_symbol> !@ast outAST {
  outAST = .default
#--- Add $bool type
  outAST.mEnumTypeList += !["bool" here] !{!["false" here], !["true" here]} !false
#---
  repeat
  while 
    <enum_declaration> !?outAST
  while 
    <enum_function> !?outAST
  while 
    <machine> !?outAST
  while 
    <scenario> !?outAST
  end
}

#--------------------------------------------------------------------*

rule <scenario>
  ?!@ast ioAST {
  $scenario$
  $identifier$ ?let @lstring machineTypeName
  $literal_string$ ?let @lstring scenarioName
  ${$
  var eventSequence =@scenarioEventSequence.default
  repeat
    $identifier$ ?let @lstring eventName
    var argList =@lstringlist.emptyList{}
    $($
    repeat
    while
      $!$
      $identifier$ ?let @lstring argName
      argList += !argName
    end
    $)$
    eventSequence += !eventName !argList
  while
  end
  $}$
  ioAST.mScenarioList += !machineTypeName !scenarioName !eventSequence
}

#--------------------------------------------------------------------*

rule <enum_declaration> ?!@ast ioAST {
  $enum$
  $enum-type-name$ ?let @lstring enumName
  ${$
  var @lstringlist cstList = {}
  repeat
    $identifier$ ?let @lstring constantName
    cstList += !constantName
  while
    $,$
  end
  $}$
  ioAST.mEnumTypeList += !enumName !cstList !true
}

#--------------------------------------------------------------------*

rule <enum_function>
  ?!@ast ioAST {
  $function$
  $identifier$ ?let @lstring functionName
  $enum-type-name$ ?let @lstring operandEnumName
  $->$
  $enum-type-name$ ?let @lstring resultEnumName
  var functionValueList = @functionValueList.default
  repeat
    $identifier$ ?let @lstring operandConstantName
    $:$
    $identifier$ ?let @lstring resultConstantName
    functionValueList += !operandConstantName !resultConstantName
  while
  end
  $}$
  ioAST.mFunctionDefinitionList += !functionName !operandEnumName !resultEnumName !functionValueList
}

#--------------------------------------------------------------------*

rule <machine> ?!@ast ioAST {
  $machine$
  $identifier$ ?let @lstring machineTypeName
#---
  var invariantExpressionList =@expressionList.emptyList{}
  var enumVarDeclarationList =@enumVarDeclarationList.default
  var eventList =@eventDeclarationList.default
  repeat
  while
    $var$
    $identifier$ ?let @lstring variableName
    $enum-type-name$ ?let @lstring enumTypeName
    $=$
    $.$
    $identifier$ ?let @lstring constantName
    enumVarDeclarationList += !enumTypeName !variableName !constantName
   while
    $invariant$
    $literal_string$ ?let @lstring lbl
    $:$
    <expression> ?let @expression expression
    invariantExpressionList += !lbl !expression
  while
    $event$
    $identifier$ ?let @lstring eventName
    $($
    var eventArgumentList =@eventArgumentList.default
    repeat
    while
      $?$
      $identifier$ ?let @lstring argumentName
      $enum-type-name$ ?let @lstring enumTypeName
      eventArgumentList += !enumTypeName !argumentName
    end
    $)$   
    ${$
    var @instructionList eventInstructionList = {}
    <instruction_list> !?eventInstructionList
    var ensureExpressionList =@expressionList.default
    select
    or
      $ensures$
      repeat
        $literal_string$ ?let @lstring lbl
        $:$
        <expression> ?let @expression expression
        ensureExpressionList += !lbl !expression
      while
      end
    end
    let @location endOfEnsureExpressionList = @location.here
    $}$
    eventList +=
      !eventName
      !eventArgumentList
      !eventInstructionList
      !ensureExpressionList
      !endOfEnsureExpressionList
  end
#---
  $}$
  ioAST.mMachineList +=
    !machineTypeName
    !enumVarDeclarationList
    !invariantExpressionList
    !eventList
}

#--------------------------------------------------------------------*

rule <expression> !@expression outExpression {
  <expression1> ?outExpression
  select
  or
    $->$
    <expression1> ?let @expression expr
    outExpression = @implicationExpression. new { !outExpression !expr}
  end
}

#--------------------------------------------------------------------*

rule <expression1> !@expression outExpression {
  <expression2> ?outExpression
  repeat
  while
    $|$
    <expression2> ?let @expression expr
    outExpression = @logicalOrExpression. new { !outExpression !expr}
  end
}

#--------------------------------------------------------------------*

rule <expression2> !@expression outExpression {
  <expression4> ?outExpression
  repeat
  while
    $&$
    <expression4> ?let @expression expr
    outExpression = @logicalAndExpression. new { !outExpression !expr}
  end
}

#--------------------------------------------------------------------*

rule <expression4> !@expression outExpression {
  $($
  <expression> ?outExpression
  $)$
}

#--------------------------------------------------------------------*

rule <expression4> !@expression outExpression {
  $identifier$ ?let @lstring propertyName
  let @enumComparisonOperator comparisonOperator
  select
    $==$
    comparisonOperator = @enumComparisonOperator.equal
  or
    $!=$
    comparisonOperator = @enumComparisonOperator.notEqual
  end
  <comparison_right_operand> ?let @comparisonRightOperand comparisonRightOperand
  outExpression = @comparisonInExpression.new {
    !propertyName
    !comparisonOperator
    !comparisonRightOperand
  }
}

#--------------------------------------------------------------------*

rule <comparison_right_operand> !@comparisonRightOperand outComparisonRightOperand {
  $.$
  $identifier$ ?let @lstring constantName
  outComparisonRightOperand = @constantAsComparisonRightOperand. new {
    !constantName}
}

#--------------------------------------------------------------------*

rule <comparison_right_operand> !@comparisonRightOperand outComparisonRightOperand {
  let @bool hasOldQualifier
  select
    hasOldQualifier = false
  or
    $old$
    hasOldQualifier = true
  end
  $identifier$ ?let @lstring propertyName
  outComparisonRightOperand = @attributeAsComparisonRightOperand. new {
    !propertyName
    !hasOldQualifier}
}

#--------------------------------------------------------------------*

rule <instruction_list> ?!@instructionList ioInstructionList {
  repeat
  while
    <instruction> ?let @instruction instruction
    ioInstructionList += !instruction
  end
}

#--------------------------------------------------------------------*

rule <instruction> !@instruction outInstruction {
  $assert$
  let @location instructionLocation = @location.here
  <expression> ?let @expression assertExpression
  outInstruction = @assertInstruction. new { !instructionLocation !assertExpression}
}

#--------------------------------------------------------------------*

rule <instruction> !@instruction outInstruction {
  $identifier$ ?let @lstring variableName
  $=$
  <assignment_source_expression> ?let @assignmentSourceExpression expression
  outInstruction = @assignmentInstruction. new { !variableName !expression}
}

#--------------------------------------------------------------------*

rule <assignment_source_expression> !@assignmentSourceExpression outExp {
  $identifier$ ?let @lstring propertyName
  outExp = @varAsAssignmentSourceExpression. new { !propertyName}
}

#--------------------------------------------------------------------*

rule <assignment_source_expression> !@assignmentSourceExpression outExp {
  $.$
  $identifier$ ?let @lstring constantName
  outExp = @constantAsAssignmentSourceExpression. new { !constantName}
}

#--------------------------------------------------------------------*

rule <assignment_source_expression> !@assignmentSourceExpression outExp {
  $identifier$ ?let @lstring functionName
  $($
  $identifier$ ?let @lstring propertyName
  $)$
  outExp = @functionAsAssignmentSourceExpression. new { !functionName !propertyName}
}

#--------------------------------------------------------------------*

#rule <instruction> !@instruction outInstruction {
#  $switch$
#  $identifier$ ?let @lstring variableName
#  ${$
#  @switchCaseList switchCaseList = {} 
#  repeat
#    $case$
#    @lstringlist constantNameList = {}
#    repeat
#      $.$
#      $identifier$ ?let @lstring constantName
#      constantNameList += !constantName
#    while
#      $,$
#    end
#    let endOfConstantList = @location.here
#    $:$
#    @instructionList caseInstructionList = {}
#    <instruction_list> !?caseInstructionList
#    switchCaseList += !constantNameList !endOfConstantList !caseInstructionList
#  while
#  end
#  outInstruction = @switchInstruction.new {
#    !variableName.location
#    !variableName
#    !switchCaseList
#    !.here
#  }
#  $}$
#}

#--------------------------------------------------------------------*

rule <instruction> !@instruction outInstruction {
  $if$
  let @location IFinstructionLocation = @location.here
  <expression> ?let @expression testExpression
  let @location endOfTestExpression = @location.here
  ${$
  var @instructionList thenInstructionList = {}
  <instruction_list> !?thenInstructionList
  $}$
  <else_part> ?let @instructionList elseInstructionList
  outInstruction = @ifInstruction.new {
    !IFinstructionLocation
    !testExpression
    !endOfTestExpression
    !thenInstructionList
    !elseInstructionList
  }
}

#--------------------------------------------------------------------*

rule <else_part> !@instructionList outInstructionList {
  outInstructionList = {}
}

#--------------------------------------------------------------------*

rule <else_part> !@instructionList outInstructionList {
  $else$
  ${$
  outInstructionList = {}
  <instruction_list> !?outInstructionList
  $}$
}

#--------------------------------------------------------------------*

rule <else_part> !@instructionList outInstructionList {
  $else$
  $if$
  let @location IFinstructionLocation = @location.here
  <expression> ?let @expression testExpression
  let @location endOfTestExpression = @location.here
  ${$
  var @instructionList thenInstructionList = {}
  <instruction_list> !?thenInstructionList
  $}$
  <else_part> ?let @instructionList elseInstructionList
  outInstructionList = {}
  outInstructionList += !@ifInstruction. new { 
    !IFinstructionLocation
    !testExpression
    !endOfTestExpression
    !thenInstructionList
    !elseInstructionList}
}

#--------------------------------------------------------------------*

}