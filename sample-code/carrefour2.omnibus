
enum $couleur {vert, orange, rouge}

#--------------------------------------------------------*

function succ $couleur -> $couleur
  vert : orange
  orange : rouge
  rouge : vert
}

#--------------------------------------------------------*

enum $Souhait {
  hs, # Hors service
  es  # En service
}

#--------------------------------------------------------*

enum $Etat {
  etat_hs, # Hors service
  etat_es, # En service
  versES, # Demande en service enregistrÃ©e
  versHS # Demande hors service enregistrÃ©e
}

#--------------------------------------------------------*

machine Carrefour

  var souhait $Souhait = .hs
  var etat $Etat = .etat_hs
  var feuA $couleur = .orange
  var feuB $couleur = .orange

 invariant "1" : (etat == .etat_hs) -> ((feuA == .orange) & (feuB == .orange))
 invariant "2" : (etat == .versES) -> ((feuA == .rouge) & (feuB == .rouge))
 invariant "3" : (etat == .etat_es) -> (((feuA == .rouge) | (feuB == .rouge)) & (feuA != feuB))
 invariant "4" : (etat == .versHS) -> ((feuA == .rouge) & (feuB == .rouge))

  event miseEnService () {
    souhait = .es
  ensures
    "A" : (feuA == old feuA)
    "B" : (feuB == old feuB)
    "C" : (etat == old etat)
    "D" : (souhait == .es)
  }

  event miseHorsService () {
    souhait = .hs
  ensures
    "A" : (feuA == old feuA)
    "B" : (feuB == old feuB)
    "C" : (etat == old etat)
    "D" : (souhait == .hs)
  }

  event tick () {
    if (etat == .etat_es) & (souhait == .hs) {
      etat = .versHS
      feuA = .rouge
      feuB = .rouge
    }else if (etat == .versHS) & (souhait == .hs) {
      etat = .etat_hs
      feuA = .orange
      feuB = .orange
    }else if (etat == .versES) & (souhait == .hs) {
      etat = .etat_hs
      feuA = .orange
      feuB = .orange
    }else if (etat == .versES) & (souhait == .es) {
      etat = .etat_es
      feuA = .vert
    }else if (etat == .versHS) & (souhait == .es) {
      etat = .etat_es
      feuA = .vert
    }else if (etat == .etat_hs) & (souhait == .es) {
      etat = .versES
      feuA = .rouge
      feuB = .rouge
    }else if (etat == .etat_es) & (souhait == .es) {
      if (feuA == .vert) {
        feuA = .orange
      }else if (feuA == .orange) {
        feuA = .rouge
        feuB = .vert
      }else if (feuB == .vert) {
        feuB = .orange
      }else{
        feuA = .vert
        feuB = .rouge
      }
    }
  }

}

#--------------------------------------------------------*
